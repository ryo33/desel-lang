not = "!";
minus = "-";
and = "&";
prefix-of-set = "@";
prefix-of-element = "%";
w = ? whitespace characters ?;
ws = { w };
wws = w, ws;
newline = ? newline characters ?;
ws-newline = ws, newline;
printables = { ? printable UTF-8 characters ? };
utf-characters = { ? all UTF-8 characters ? };
not-ws = not, ws;
not-wws = not, wws;
opt-not-ws = [ not-ws ];
opt-not-wws = [ not-wws ];

statements = { statement };
statement = set-definition |
            element-definition |
            sets-definition |
            elements-definition |
            comment;

expression = opt-not-ws, "(", ws, expression, ws, ")"
           | expression-and, { ( ws, minus, ws | wws ), expression-and };
expression-and = expression-min, { ws, and, ws, expression-min };
expression-min = opt-not-ws, set-with-prefix;

set-definition = set-with-prefix, [ homonymous-element ],
                 { wws, set-definition-item }, ws-newline,
                 { additional-set-definition, ws-newline };
additional-set-definition = prefix-of-set, { wws, set-definition-item };
set-definition-item = opt-not-wws, element
                    | opt-not-ws, element-with-prefix
                    | expression;

element-definition = element-with-prefix, [ homonymous-set ],
                     { wws, element-definition-item, }, ws-newline,
                     { additional-element-definition, ws-newline };
additional-element-definition = prefix-of-element, { wws, element-definition-item };
element-definition-item = opt-not-wws, set
                        | opt-not-ws, set-with-prefix;

sets-definition = 2 * prefix-of-set,
                  { wws, sets-definition-item }, ws-newline;
sets-definition-item = set, [ homonymous-element ],
                       { wws, opt-not-ws, element-with-prefix };

elements-definition = 2 * prefix-of-element,
                      { wws, elements-definition-item }, ws-newline;
elements-definition-item = element, [ homonymous-set ],
                           { wws, opt-not-ws, set-with-prefix };

homonymous-set = ws, prefix-of-element;
homonymous-element = ws, prefix-of-element;

set = [ prefix-of-set ], label;
element = [ prefix-of-element ], label;
set-with-prefix = prefix-of-set, label;
element-with-prefix = prefix-of-element, label;
label = '"', utf-characters, '"'
      | "'", utf-characters, "'"
      | printables;
