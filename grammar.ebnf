not = "!".
minus = "-".
and = "&".
prefix_of_set = "%".
prefix_of_element = "@".
prefix_of_inline_comment = "#".
prefix_of_comment = . // ? all UTF-8 characters excluding `@` and `%` ?.
w = . // ? whitespace character ?.
Ws = { w }.
Wws = w Ws.
newline = . // ? newline characters ?.
WsNewline = Ws newline.
label_characters = . // ? printable UTF-8 encoded characters excluding '!' '-' '&' '@' '%' '(' ')' and '#' ?.
utf_characters = . // ? UTF-8 encoded characters escaped properly excluding newline characters ?.
NotWs = not Ws.
NotWws = not Wws.
OptNotWs = [ NotWs ].
OptNotWws = [ NotWws ].

Statements = { Statement }.
Statement = SetDefinition |
            ElementDefinition |
            SetsDefinition |
            ElementsDefinition |
            Comment.

Expression = OptNotWs WrappedExpression
           | ExpressionAnd { ( Ws minus Ws | Wws ) ExpressionAnd }.
ExpressionAnd = ExpressionMin { Ws and Ws ExpressionMin }.
ExpressionMin = SetWithPrefixAndOptNot.
WrappedExpression = "(" Ws Expression Ws ")".

SetDefinition = SetWithPrefix [ HomonymousElement ]
                 { Wws SetDefinitionItem } InlineComment WsNewline
                 { Comment | AdditionalSetDefinition InlineComment WsNewline }.
AdditionalSetDefinition = prefix_of_set { Wws SetDefinitionItem }.
SetDefinitionItem = ElementWithOptNot HomonymousSet
                    | Expression.

ElementDefinition = ElementWithPrefix [ HomonymousSet ]
                     { Wws ElementDefinitionItem } InlineComment WsNewline
                     { Comment | AdditionalElementDefinition InlineComment WsNewline }.
AdditionalElementDefinition = prefix_of_element { Wws ElementDefinitionItem }.
ElementDefinitionItem = SetWithOptNot.

SetsDefinition = prefix_of_set prefix_of_set
                  { Wws SetsDefinitionItem }
                  InlineComment WsNewline.
SetsDefinitionItem = Set [ HomonymousElement ]
                       { Wws ( ElementWithPrefixAndOptNot | WrappedExpression ) }.

ElementsDefinition = prefix_of_element prefix_of_element
                      { Wws ElementsDefinitionItem }
                      InlineComment WsNewline.
ElementsDefinitionItem = Element [ HomonymousSet ]
                           { Wws SetWithPrefixAndOptNot }.

HomonymousSet = Ws prefix_of_set.
HomonymousElement = Ws prefix_of_element.

Set = [ prefix_of_set ] Label.
Element = [ prefix_of_element ] Label.
SetWithPrefix = prefix_of_set Label.
ElementWithPrefix = prefix_of_element Label.
SetWithOptNot = SetWithPrefixAndOptNot
                 | LabelWithOptNot.
SetWithPrefixAndOptNot = OptNotWs ElementWithPrefix.
ElementWithOptNot = ElementWithPrefixAndOptNot
                     | LabelWithOptNot.
ElementWithPrefixAndOptNot = OptNotWs ElementWithPrefix.
LabelWithOptNot = OptNotWs WrappedLabel
                   | OptNotWws Label.
Label = label_characters | WrappedLabel.
WrappedLabel = "\"" utf_characters "\""
              | "'" utf_characters "'".
InlineComment = Ws prefix_of_inline_comment utf_characters.
Comment = prefix_of_comment utf_characters newline.
